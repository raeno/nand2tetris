// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=tempx1);
    Not16(in=tempx1, out=negx);
    Mux16(a=tempx1, b=negx, sel=nx, out=tempx2);

    Mux16(a=y, b=false, sel=zy, out=tempy1);
    Not16(in=tempy1, out=negy);
    Mux16(a=tempy1, b=negy, sel=ny, out=tempy2);

    // sum or bit-and
    Add16(a=tempx2, b=tempy2, out=sumxy);
    And16(a=tempx2, b=tempy2, out=andxy);
    Mux16(a=andxy, b=sumxy, sel=f, out=tempout);
    Not16(in=tempout, out=negtempout);

    Mux16(a=tempout, b=negtempout, sel=no, out[0..7]=rightHandOut, out[8..15]=leftHandOut, out[15]=ng, out=out);
    Or8Way(in=rightHandOut, out=outNonZero1);
    Or8Way(in=leftHandOut, out=outNonZero2);
    Or(a=outNonZero1, b=outNonZero2, out=outNonZero);
    Not(in=outNonZero, out=zr);
}